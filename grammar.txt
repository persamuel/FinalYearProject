Goal 				::= (FunctionDeclaration)* MainFunction (FunctionDefinition)* <EOF>

FunctionSignature	::= SignatureType Identifier "(" (SignatureType Identifier ("," SignatureType Identifier)*))? ")"

FunctionDeclaration ::= FunctionSignature ";"

FunctionDefinition	::=	FunctionSignature "{" FunctionBody "}"

FunctionBody 		::= (VarDeclaration)* (Statement)* "return" Expression

MainFunction 		::= "int" "main" "(" "int" Identifier "," "char" "*" Identifier "[" "]" ")" "{" FunctionBody "}"
	
VarDeclaration		::= Type Identifier ";"

Type				::= StackArray
						| HeapArray
						| BasicType

SignatureType 		::= BasicType 
						| HeapArray

BasicType			::= "int"
						| "char"
						| "boolean"
						
StackArray 			::= "int" "[" <INTEGER_LITERAL> "]"
						| "char" "[" <INTEGER_LITERAL> "]" 	
						
HeapArray 			::= "int" "[" "]"
						| "char" "[" "]"

Statement			::= "{" (Statement)* "}"
						| "if" "(" Expression ")" Statement "else" Statement
						| "while" "(" Expression ")" Statement
						| Identifier "=" Expression ";"
						| Identifier "[" Expression "]" "=" Expression ";"
						| "free" Identifier ";"
						| "print" "(" Expression ")" ";"
						
Expression 			::= Expression ("&&" | "||" | "!=" | "==" | "<" | "<=" | ">" | ">=" | "+" | "-" | "*") Expression
						| Expression "[" Expression "]"
						| Identifier "(" (Expression ("," Expression)*)? ")"
						| <INTEGER_LITERAL>
						| <CHARACTER_LITERAL>
						| <BOOLEAN_LITERAL>
						| Identifier
						| "new" "int" "[" Expression "]"
						| "new "char" "[" Expression "]"
						| "!" Expression
						| "(" Expression ")"

Identifier			::= <IDENTIFIER>